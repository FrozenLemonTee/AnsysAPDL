!圆柱直齿轮副的接触分析 MPC 算法接触处理
FINISH
/CLEAR
/FILNAME, GEAR
/PREP7

MP, EX, 1, 2E5
MP, PRXY, 1, 0.3
MP, DENS, 1, 8E-12
MP, MU, 1, 0.2

ET, 1, SOLID185
ET, 2, PLANE182

KEYOPT, 2, 1, 1
R, 2, , 4


*SET, RB, 30    !基圆半径
*SET, RA, 40    !大径长之半
*SET, DELTA, 10   !细分次数
*SET, THETA, 5    !渐开线展角

*SET, OMEGA, THETA/DELTA
*DO, i, 0, DELTA, 1
*SET, LR, -0.4*OMEGA*i*OMEGA*i    !极轴长
LOCAL, 11, 0, 0, 0, 0, OMEGA*i
CSYS, 11
K, , LR+RA, 0, 0
*ENDDO

BSPLIN, ALL
CSYS, 0
LOCAL, 11, 1
LGEN, 2, ALL, , , 0, 5, 0, , , 1
K, , 0, 0, 0
*GET, TMP1, KP, , NUM, MAX
CSYS, 0
CIRCLE, TMP1, RA, , , 5
CIRCLE, TMP1, RB, , , 5
*GET, TMP1, LINE, , NUM, MAX
CSYS, 11
LGEN, 2, TMP1, TMP1, 1, 0, 10, 0, , , 1
CSYS, 0

LSYMM, Y, ALL
CSYS, 11
LGEN, 12, ALL, , , 0, 30, 0
LGLUE, ALL
AL, ALL

CSYS, 0
CYL4, 0, 0, RA/4, 0, 0, 360
*GET, TMP1, AREA, , NUM, MIN
*GET, TMP2, AREA, , NUM, MAX
ASBA, TMP1, TMP2
BLC5, 0, RA/4, RA/4, RA/8
*GET, TMP1, AREA, , NUM, MIN
*GET, TMP2, AREA, , NUM, MAX
ASBA, TMP2, TMP1

LGLUE, ALL
*SET, MOVE, RA/8

CSYS, 0
AGEN, 2, ALL, , , 14*RA/8+MOVE
*GET, TMP1, AREA, , NUM, MIN
LOCAL, 11, 1, 14*RA/8+MOVE, 0, 0
AGEN, 2, TMP1, TMP1, 1, 0, 195, 0, , , 1

CSYS, 0
LSEL, S, LOC, X, RA/2, 3*RA/2+MOVE
LESIZE, ALL, , , 12
LSEL, S, LOC, X, -RA, RA/2
LSEL, A, LOC, X, 3*RA/2+MOVE, 5*RA/2+MOVE
LESIZE, ALL, , , 4
AATT, 1, -1, 2
AMESH, ALL

NUMMRG, ALL, , , , LOW

*GET, TMP1, AREA, , NUM, MAX
*GET, TMP2, AREA, , NUM, MIN
ASEL, S, , , TMP1, TMP1, 1
LOCAL, 11, 1
LSLA, S
LSEL, R, LOC, Y, 0, 10
NSLL, S
*SET, EID, 3
*SET, RID, 3
ET, EID, TARGE169
TYPE, EID
R, RID
REAL, RID
ESURF

*SET, EID, EID+1
ASEL, S, , , TMP2, TMP2, 1
LOCAL, 11, 1, 14*RA/8+MOVE, 0, 0
LSLA, S
LSEL, R, LOC, Y, 170, 180
NSLL, S
ET, EID, CONTA172
KEYOPT, EID, 2, 2
KEYOPT, EID, 12, 5
TYPE, EID
REAL, RID
ESURF

/SOLU
ANTYPE, 0
NLGEOM, 1
LOCAL, 11, 1
ASEL, S, , , TMP1, TMP1, 1
LSLA, S
LSEL, R, LOC, X, 0, RA/2
NSLL, S
D, ALL, UX
D, ALL, UY, -0.2

LOCAL, 11, 1, 14*RA/8+MOVE, 0, 0
ASEL, S, , , TMP2, TMP2, 1
LSLA, S
LSEL, R, LOC, X, 0, RA/2
NSLL, S
D, ALL, ALL

CSYS, 0
ALLSEL, ALL
SOLVE

/POST1
PLNSOL, S, EQV
