!圆柱斜齿轮副的接触分析 MPC 算法接触处理
FINISH
/CLEAR
/FILNAME, GEAR
/PREP7

MP, EX, 1, 2E5
MP, PRXY, 1, 0.3
MP, DENS, 1, 8E-9
MP, MU, 1, 0.2

ET, 1, SOLID185
ET, 2, PLANE182

KEYOPT, 2, 1, 1
R, 2, , 4

*SET, RB, 30    !基圆半径
*SET, RA, 40    !大径长之半
*SET, DELTA, 10   !细分次数
*SET, THETA, 5    !渐开线展角
*SET, H, 10   !齿厚

*SET, OMEGA, THETA/DELTA
*DO, i, 0, DELTA, 1
*SET, LR, -0.4*OMEGA*i*OMEGA*i    !极轴长
LOCAL, 11, 0, 0, 0, 0, OMEGA*i
CSYS, 11
K, , LR+RA, 0, 0
*ENDDO

BSPLIN, ALL
CSYS, 0
LOCAL, 11, 1
LGEN, 2, ALL, , , 0, 5, 0, , , 1
K, , 0, 0, 0
*GET, KP1, KP, , NUM, MAX
CSYS, 0
CIRCLE, KP1, RA, , , 5
CIRCLE, KP1, RB, , , 5
*GET, KP1, LINE, , NUM, MAX
CSYS, 11
LGEN, 2, KP1, KP1, 1, 0, 10, 0, , , 1
CSYS, 0

LSYMM, Y, ALL
CSYS, 11
LGEN, 12, ALL, , , 0, 30, 0
LGLUE, ALL
AL, ALL

LGLUE, ALL
*SET, MOVE, RA/8-RA/32    !齿轮中心距微调量
*SET, A0, 14*RA/8+MOVE   !齿轮副中心距
*SET, DTHETA, 10    !齿轮相错角

CSYS, 0
AGEN, 2, ALL, , , A0
*GET, AREA1, AREA, , NUM, MIN
LOCAL, 11, 1, A0, 0, 0
AGEN, 2, AREA1, AREA1, 1, 0, 0, 0, , , 1


*DO, j, 0, 1, 1
*GET, KP1, KP, , NUM, MAX
CSYS, 0
K, KP1+1, RA+A0*j
*GET, KP1, KP, , NUM, MAX
*SET, DH, H/DELTA   !Z轴方向每次移动长度

LSEL, NONE
*DO, i, 0, DELTA-1, 1
LOCAL, 11, 1, A0*j, 0, DH*(i+1)*(-1)**j, OMEGA*(i+1)
CSYS, 11
K, KP1+1, RA
L, KP1, KP1+1
*SET, KP1, KP1+1
*ENDDO

CSYS, 0
LCOMB, ALL
*IF, j, EQ, 0, THEN
*GET, LINE1, LINE, , NUM, MAX
*ELSE
*GET, LINE2, LINE, , NUM, MAX
*ENDIF
CSYS, 11
*ENDDO

*GET, AREA1, AREA, , NUM, MIN
*GET, AREA2, AREA, , NUM, MAX

ALLSEL, ALL
*DO, j, 0, 1, 1
CSYS, j*11
VDRAG, (-j+1)*AREA1+j*AREA2, , , , , , (-j+1)*LINE1+j*LINE2
*ENDDO

*GET, VOLU1, VOLU, , NUM, MAX
*GET, VOLU2, VOLU, , NUM, MIN
LOCAL, 11, 1, A0, 0, 0
VGEN, 2, VOLU1, VOLU1, 1, , DTHETA, 0, , , 1
LOCAL, 11, 0, A0, 0, 0
VGEN, 2, VOLU1, VOLU1, 1, , 0, H, , , 1

CSYS, 0
VSEL, NONE
CYL4, 0, 0, RA/4, 0, 0, 360, H
BLC5, 0, RA/4, RA/4, RA/8, H
VADD, ALL
VGEN, 2, ALL, , , A0, 0, 0
*GET, VOLU3, VOLU, , NUM, MIN
LOCAL, 11, 1, A0, 0, 0
VGEN, 2, VOLU3, VOLU3, 1, , DTHETA, 0, , , 1
*GET, VOLU3, VOLU, , NUM, MAX
*GET, VOLU4, VOLU, , NUM, MIN

ALLSEL, ALL
CSYS, 0
VSBV, VOLU1, VOLU4
VSBV, VOLU2, VOLU3

LSEL, S, LOC, Z, H
LESIZE, ALL, , , 8
ASEL, S, LOC, Z, H
AATT, 1, -1, 2
AMESH, ALL

*GET, VOLU3, VOLU, , NUM, MAX
*GET, VOLU4, VOLU, , NUM, MIN

VSWEEP, VOLU3
VSWEEP, VOLU4

*SET, EID, 3
*SET, RID, 3

!添加接触对
VSEL, S, , , VOLU4, VOLU4, 1
ASLV, S
LOCAL, 11, 1
ASEL, R, LOC, Z, H/4, 3*H/4
ASEL, U, LOC, X, 0, RA/2
ASEL, R, LOC, Y, 2.5, 15
NSLA, S
ET, EID, TARGE170
TYPE, EID
R, RID
REAL, RID
ESURF

*SET, EID, EID+1
LOCAL, 11, 1, A0
VSEL, S, , , VOLU3, VOLU3, 1
ASLV, S
ASEL, R, LOC, Z, H/4, 3*H/4
ASEL, U, LOC, X, 0, RA/2
ASEL, R, LOC, Y, 182.5, 190
NSLA, S
ET, EID, CONTA174
KEYOPT, EID, 2, 2
KEYOPT, EID, 12, 5
TYPE, EID
REAL, RID
ESURF

*SET, EID, EID+1
*SET, RID, RID+1
!添加接触对
VSEL, S, , , VOLU4, VOLU4, 1
ASLV, S
LOCAL, 11, 1
ASEL, R, LOC, Z, H/4, 3*H/4
ASEL, U, LOC, X, 0, RA/2
ASEL, R, LOC, Y, -7.5, 0
NSLA, S
ET, EID, TARGE170
TYPE, EID
R, RID
REAL, RID
ESURF

*SET, EID, EID+1
LOCAL, 11, 1, A0
VSEL, S, , , VOLU3, VOLU3, 1
ASLV, S
ASEL, R, LOC, Z, H/4, 3*H/4
ASEL, U, LOC, X, 0, RA/2
ASEL, R, LOC, Y, 180, 190
NSLA, S
ET, EID, CONTA174
KEYOPT, EID, 2, 2
KEYOPT, EID, 12, 5
TYPE, EID
REAL, RID
ESURF

NUMMRG, ALL, , , , LOW

!孔内加边界条件
LOCAL, 11, 1
VSEL, S, , , VOLU4, VOLU4, 1
ASLV, S
ASEL, R, LOC, X, 0, RA/2
ASEL, U, LOC, Z, 0
ASEL, U, LOC, Z, H
NSLA, S
D, ALL, UX
CSWPLA, 11, CYLIN
NROTAT, ALL
D, ALL, UY, -0.2
D, ALL, UZ

LOCAL, 11, 1, A0
VSEL, S, , , VOLU3, VOLU3, 1
ASLV, S
ASEL, R, LOC, X, 0, RA/2
ASEL, U, LOC, Z, 0, 0.1
ASEL, U, LOC, Z, H
NSLA, S
D, ALL, ALL

/SOLU
ANTYPE, 0
NLGEOM, 1

CSYS, 0
ALLSEL, ALL
SOLVE

/POST1
PLNSOL, S, EQV
